#------------------------------------------------------------------------------#
# File: Configure.ac
# Original project: BTRunit
# Original author: Nicholas Clark
# File license: GPL 2.0
#------------------------------------------------------------------------------#

#--------------------------- Load Project Version -----------------------------#

# This needs to happen before calling AC_INIT, so the relevant macro must be
# manually included.

m4_include([m4/semantic_version.m4])
SEMANTIC_VERSION_SET_FILE([VERSION])

#------------------------- Initialize Autoconf Project ------------------------#

AC_PREREQ([2.69])
AC_INIT([btrunit], SEMANTIC_VERSION, [nicholas.clark@gmail.com])
AC_SUBST([LIB_RELEASE],[SEMANTIC_VERSION])

AC_CONFIG_MACRO_DIR([m4])
AC_CONFIG_AUX_DIR([build-aux])
AC_CONFIG_SRCDIR([src/sv.c])
AC_CONFIG_HEADERS([config.h])

AM_INIT_AUTOMAKE([foreign subdir-objects serial-tests -Wall -Werror])
AC_USE_SYSTEM_EXTENSIONS

#------------------------- Run Basic Feature Checks  --------------------------#

AC_PROG_CC
AC_PROG_INSTALL
AM_PROG_AR
AC_PROG_RANLIB

AC_C_INLINE

AC_TYPE_UINT64_T
AC_TYPE_MODE_T
AC_TYPE_UID_T
AC_TYPE_OFF_T

#--------------------- Run System Library / Header Checks ---------------------#

# Checks for system headers

AC_CHECK_HEADERS([fcntl.h netinet/in.h stdlib.h string.h sys/file.h \
                  sys/ioctl.h sys/param.h sys/socket.h sys/time.h unistd.h \
                  utmp.h utmpx.h grp.h sys/select.h sys/reboot.h poll.h])

AC_HEADER_DIRENT

# Checks for library functions.

AC_FUNC_FORK
AC_FUNC_GETGROUPS
AC_FUNC_MALLOC

AC_CHECK_FUNCS([dup2 fchdir ftruncate gettimeofday memset mkdir mkfifo \
                select socket flock sigprocmask sigaction waitpid])

#--------------------- Runit Custom Configuration Checks ----------------------#

# The wait3() function has been obsolete for a while now. However, legacy
# runit does support it as an alternative to waitpid(). The generated
# configure script will try to find waitpid() first, and will only scan for
# wait3() if waitpid() can't be found.

AS_IF([test "x$ac_cv_func_waitpid" = xyes],,[AC_FUNC_WAIT3])

# This comes from the recipe for the original 'socket.lib' flag file
# generated by legacy runit's Makefile. They're probably not necessary
# on any modern system, but are left here for compatability.

AC_SEARCH_LIBS(socket, [xnet socket])
AS_IF([test "x$ac_cv_search_socket" = "x-lsocket"],
      [AS_VAR_APPEND([LIBS],[" -lnsl"])])

# Upstream runit is willing to use a reboot() with two arguments instead of
# just one. The correct arg-count is chosen by the custom AX macro called below.

AX_GUESS_REBOOT_NARGS

# Upstream runit uses a runtime test to see whether poll() works correctly
# on files. This logic is encoded into a custom AX macro called below.

AX_CHECK_FUNC_POLL

# Upstream runit looks for a stuct named 'futmpx' which is Solaris-specific.
# The check is encoded into the custom macro below.

AX_TYPE_STRUCT_FUTMPX

#---------------------- Configure For Feature Selection  ----------------------#

AH_TOP([#ifndef __CONFIG_H__
#define __CONFIG_H__])
AH_BOTTOM([#endif])

AC_CONFIG_FILES([Makefile src/Makefile man/Makefile])
AC_OUTPUT
